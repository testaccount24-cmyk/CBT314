/* --------------------  rexx procedure  -------------------- *
 * Name:      UIDLIST                                         *
 *                                                            *
 * Function:  List all Users and their UIDs by listing        *
 *            the /u/userid directories                       *
 *                                                            *
 * Syntax:    %uidlist                                        *
 *                                                            *
 * Author:    Lionel B. Dyck                                  *
 *                                                            *
 * Dependencies                                               *
 *            1. Stemview command                             *
 *                                                            *
 * History:                                                   *
 *            05/21/16 - Creation                             *
 *                                                            *
 * ---------------------------------------------------------- */
 /*   rexx   */
 call syscalls 'ON'
 address syscall
 'readdir /u root.'
 l = 4
 lo.1 = ' OMVS Userids for:' mvsvar('sysname')
 lo.2 = ' '
 lo.3 = ' 'left('Name',10) left('uid',10) left('gid',10) 'dir'
 lo.4 = ' 'left('-',10,'-') left('-',10,'-') left('-',10,'-') ,
           left('-',10,'-')
 do i=1 to root.0
    if left(root.i,1) = '.' then iterate
    'getpwnam' root.i 'pw.'
    l = l + 1
    if pw.0 > 0 then
       lo.l =  ' 'left(root.i,10) left(pw.pw_uid,10) left(pw.pw_gid,10) ,
                  pw.pw_dir
       else
       lo.l =  ' 'left(root.i,10) left('***',10) left('***',10) 'Invalid user'
 end
 lo.0 = l

 if sysvar('sysispf') = 'ACTIVE' then
    CALL stemview "View",'lo.',,,"OMVS Userids"
 else do i = 1 to lo.0
        say lo.i
        end
