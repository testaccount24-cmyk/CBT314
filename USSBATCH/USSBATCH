/* --------------------  rexx procedure  -------------------- */
  ver = '1.1'
/* Name:      USSBATCH                                        |
 |                                                            |
 | Function:  Execute OMVS commands in batch from //STDIN DD  |
 |                                                            |
 |            This provides a more robust interface in batch  |
 |            than the provided PGM=BPXBATCH                  |
 |                                                            |
 | Sample JCL:                                                |
 |                                                            |
 |   //USSBATCH EXEC PGM=IKJEFT01,PARM='%USSBATCH'            |
 |   //SYSTSPRT DD  SYSOUT=*                                  |
 |   //SYSTSIN  DD  DUMMY                                     |
 |   //SYSEXEC  DD  DISP=SHR,DSN=T311LBD.LIONEL.EXEC          |
 |   //STDIN    DD  *                                         |
 |   * enter omvs commands here                               |
 |                                                            |
 | Usage Notes:                                               |
 |                                                            |
 |      1. Multiple commands may be on the same               |
 |         record separated by a ;                            |
 |      2. su will change the effective uid                   |
 |      3. directory references should be fully               |
 |         qualified                                          |
 |      4. cd (change directory) works but is reset           |
 |         to the users home directory with each              |
 |         command (including stacked commands)               |
 |      5. comments start with * or # in column 1             |
 |      6. Do *NOT* enable ISPF Edit sequence numbers         |
 |      7. Add a + to the end of a record to continue         |
 |         the command to the next record                     |
 |                                                            |
 | Author:    Lionel B. Dyck                                  |
 |                                                            |
 | History:  (most recent on top)                             |
 |      1.1   01/23/18 - Add + for continuation               |
 |      0.1   01/23/18 - Creation                             |
 |                                                            |
 * ---------------------------------------------------------- */

  parse value '' with null su

/* ---------------- *
 | Prime the report |
 * ---------------- */
  say 'USS Batch Processing - version:' ver 'on' date() 'at' time()
  say ' '

/* ------------------------------------------------- *
 | Read in all commands from STDIN and pass them one |
 | at a time to the processing routine.              |
 | - Note * and # in column 1 are comments           |
 | - Add a + at the end of a record to continue the  |
 |   command to the next record                      |
 * ------------------------------------------------- */
  'execio * diskr stdin (finis stem in.'
  do ioc = 1 to in.0
    cmd = strip(in.ioc)
    if right(cmd,1) = '+' then do
       ioc = ioc + 1
       cmd = left(cmd,length(cmd)-1) strip(in.ioc)
       end
    if pos(left(cmd,1),'*#') = 0 then call do_omvs
  end

  Exit 0

/* -------------------------------- *
 | Process the OMVS command:        |
 |                                  |
 | 1. split commands using the ;    |
 | 2. if su is found then set uid 0 |
 | 3. issue command using bpxwunix  |
 | 4. write all messages to the log |
 * -------------------------------- */
do_omvs:
  do until cmd = null
    parse value cmd with xcmd';'cmd
    cmd = strip(cmd)
    Select
     /* ---------------------------------- *
      | Process the su (superuser) command |
      * ---------------------------------- */
      When xcmd = 'su' then
      if su /= 1 then do
        say 'Command:' xcmd
        say ' '
        su = 1
        address syscall 'geteuid'
        myeuid=retval
        Address syscall "seteuid 0"
        say 'Changed to SuperUser uid(0) from uid('myeuid')'
        say ' '
      end
      else do
        say 'Command:' xcmd
        say ' '
        su = 0
        Address syscall "seteuid" myeuid
        rc = syscalls("OFF")
        say ' '
        say 'Changed back from SuperUser to uid('myeuid')'
      end
     /* -------------------------- *
      | Process all other commands |
      * -------------------------- */
      Otherwise do
        say 'Command:' xcmd
        erc = bpxwunix(xcmd,,stdout.,stderr.)
        if erc > maxrc then maxrc = erc
        say
        say 'Return code:' erc
        if stdout.0 > 0 then do
          say ' '
          say 'Output of command:' xcmd
          say ' '
          do ic = 1 to stdout.0
            say stdout.ic
          end
          say ' '
        end
        if stderr.0 > 0 then do
          say ' '
          say 'Error output of command:' xcmd
          say ' '
          do ic = 1 to stderr.0
            say stderr.ic
          end
        end
        drop stdout. stderr.
      end
    end
  end
  return
